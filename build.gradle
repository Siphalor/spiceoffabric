plugins {
	id 'fabric-loom' version '0.8-SNAPSHOT'
	id 'maven-publish'
	id 'java-library'
	id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
	id 'com.modrinth.minotaur' version '1.2+'
	id "com.github.breadmoirai.github-release" version "2.2.12"
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version + '+' + minecraft_version
group = project.maven_group

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://maven.terraformersmc.com/releases" }
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.siphalor.de" }
	maven { url "https://maven.ryanliptak.com/" }
	maven { url 'https://jitpack.io' }
}

minecraft {
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	
	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	
	// exp4j
	shadow(implementation('net.objecthunter:exp4j:0.4.8'))

	// Tweed
	include(modApi("de.siphalor.tweed4:tweed4-base-$minecraft_major_version:1.3.1+"))
	include(modApi("de.siphalor.tweed4:tweed4-annotated-$minecraft_major_version:1.2.0+"))
	include(modApi("de.siphalor.tweed4:tweed4-data-$minecraft_major_version:1.2.0+"))
	include(modApi("de.siphalor.tweed4:tweed4-data-hjson-$minecraft_major_version:1.1.0+"))
	include(modApi("de.siphalor.tweed4:tweed4-tailor-screen-$minecraft_major_version:1.0.1+"))
	include(modApi("de.siphalor.tweed4:tweed4-tailor-coat-$minecraft_major_version:1.0.1+"))
	include(modApi("de.siphalor:coat-$minecraft_major_version:1.0.0-beta.11.dev"))

	// AppleSkin
	modImplementation "squeek.appleskin:appleskin-fabric:mc1.17-2.1.3"

	// NBT Crafting
	include(modImplementation ("de.siphalor:nbtcrafting-$minecraft_major_version:${nbtcrafting_version}") {
		exclude group: ("net.fabricmc.fabric-api")
		exclude module: "modmenu"
		exclude module: "RoughlyEnoughItems"
		exclude module: "ModsOfTheWorld"
	})

	// REI
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:6.0+"
	modRuntime "me.shedaniel:RoughlyEnoughItems-fabric:6.0+"

	// LazyDFU
	modRuntime("com.github.astei:lazydfu:master-SNAPSHOT")
}

processResources {
	inputs.property "version", version

	afterEvaluate {
		from(sourceSets.main.resources.srcDirs) {
			include "fabric.mod.json"
			expand "version": version
			duplicatesStrategy DuplicatesStrategy.INCLUDE
		}
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	archiveClassifier.set("sources")
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
	archiveClassifier.set("dev")
}

shadowJar {
	configurations = [project.configurations.shadow]
	archiveClassifier.set("dev")
	relocate "net.objecthunter", "de.siphalor.spiceoffabric.shadow.net.objecthunter"
}

remapJar {
	dependsOn(shadowJar)
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = "${project.archives_base_name}-${project.minecraft_major_version}"
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}
	repositories {
		if (project.hasProperty("siphalorMavenUser")) {
			maven {
				name = "Siphalor"
				url = "https://maven.siphalor.de/upload.php"
				credentials {
					username = siphalorMavenUser
					password = siphalorMavenPassword
				}
			}
		}
	}
}

// Mod sites

def getChangelog() {
	def commits = 'git log -20 --format=oneline'.execute().text.readLines()
	def versionCommit = null
	for (commit in commits) {
		if (commit.contains('Version')) {
			versionCommit = commit.split(' ')[0]
			break
		}
	}
	if (versionCommit == null) {
		return """## Version ${project.mod_version}

No changelog found!"""
	}
	return ('git log -1 --format=format:##%x20%s%n%n%b%nRelease%x20by%x20%an ' + versionCommit).execute().text.trim()
}

task uploadToModSites {
	dependsOn build
	group = "upload"
}

if (project.hasProperty("siphalorCurseForgeApi")) {
	curseforge {
		apiKey project.siphalorCurseForgeApi
		def changes = getChangelog()
		project {
			id = "318416"
			releaseType = project.mod_release
			changelogType = "markdown"
			changelog = changes
			addGameVersion('Fabric')
			for (version in ((String) project.mod_mc_versions).split(";")) {
				addGameVersion(version)
			}
			relations {
				requiredDependency "fabric-api"
				optionalDependency "appleskin"
				embeddedLibrary "nbt-crafting"
				optionalDependency "roughly-enough-items"
				embeddedLibrary "cloth-config"
			}
			mainArtifact(remapJar) {
				displayName = "[${project.mod_mc_version_specifier}] ${project.mod_version}"
			}
		}
	}
	uploadToModSites.finalizedBy(tasks.curseforge)
}

import com.modrinth.minotaur.TaskModrinthUpload
if (project.hasProperty("siphalorModrinthApi")) {
	task modrinth (type: TaskModrinthUpload) {
		group = "upload"
		dependsOn(build)

		token = project.siphalorModrinthApi
		projectId = "roxihOCb"
		versionNumber = version
		versionName = "[${project.mod_mc_version_specifier}] ${project.mod_version}"
		changelog = getChangelog()
		uploadFile = remapJar
		versionType = project.mod_release
		for (version in ((String) project.mod_mc_versions).split(";")) {
			addGameVersion(version)
		}
		addLoader("fabric")
	}
	uploadToModSites.finalizedBy(tasks.modrinth)
}

if (project.hasProperty("siphalorGitHubToken")) {
	githubRelease {
		token siphalorGitHubToken
		targetCommitish.set("master")
		releaseName.set("Version $mod_version")
		body.set(getChangelog())
		releaseAssets remapJar.getArchiveFile()
		prerelease.set(mod_release != "release")
		overwrite.set(true)
	}
	uploadToModSites.finalizedBy(tasks.githubRelease)
}
