plugins {
	id 'fabric-loom' version '0.5-SNAPSHOT'
	id 'maven-publish'
	id 'java-library'
	id 'com.github.johnrengelman.shadow' version '4.0.0'
}

evaluationDependsOnChildren()

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version + '+' + minecraft_version
group = project.maven_group

repositories {
	mavenLocal()
    maven { url "https://minecraft.curseforge.com/api/maven"}
	mavenCentral()
	maven { url "https://maven.siphalor.de" }
	maven { url 'https://jitpack.io' }
}

minecraft {
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	
	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	
	// exp4j
	runtime 'net.objecthunter:exp4j:0.4.8'
	shadow 'net.objecthunter:exp4j:0.4.8'
	
	// Tweed
	include "${project.tweed_maven}:${project.tweed_version}"
	modImplementation ("${project.tweed_maven}:${project.tweed_version}") {
		exclude group: ("net.fabricmc.fabric-api")
		exclude module: "modmenu"
	}
	
	// AppleSkin
	modImplementation ('com.github.squeek502:AppleSkin:1.16-fabric-SNAPSHOT') {
		exclude group: ("net.fabricmc.fabric-api")
		exclude module: "modmenu"
		transitive false
	}
	
	// NBT Crafting
	include "de.siphalor:nbtcrafting-1.16:${nbtcrafting_version}"
	modImplementation ("de.siphalor:nbtcrafting-1.16:${nbtcrafting_version}") {
		exclude group: ("net.fabricmc.fabric-api")
		exclude module: "modmenu"
		exclude module: "RoughlyEnoughItems"
		exclude module: "ModsOfTheWorld"
	}
	// REI
	modImplementation ("me.shedaniel:RoughlyEnoughItems:5+") {
		exclude group: ("net.fabricmc.fabric-api")
		exclude module: "modmenu"
	}

	// Modmenu
	modImplementation ("io.github.prospector:modmenu:1.14.13+") {
		force = true
		transitive false
	}

	// Cloth Config
	include "me.shedaniel.cloth:config-2:${project.clothconfig_version}"
	modImplementation ("me.shedaniel.cloth:config-2:${project.clothconfig_version}") {
		exclude group: "net.fabricmc.fabric-api"
		transitive false
	}
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
	classifier = ""
}

shadowJar {
	configurations = [project.configurations.shadow]
	classifier = "dev"
	relocate "net.objecthunter", "de.siphalor.spiceoffabric.shadow.net.objecthunter"
}

remapJar {
	dependsOn(shadowJar)
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}
